MODE1:HIMEM=&2B00

ONERRORCOLOUR128:COLOUR3:PRINTTAB(0,28);:REPORT:PRINT" basic line ";ERL;:PROCa:PROCm

PROCi
PROCds

PROCm

END


DEFPROCm

REPEAT

PROCdc
PROCpk

UNTILFALSE

ENDPROC



DEFPROCds

CLS

FORi%=0TO17:PROCdt(G%+i%,H%,i%):NEXT

PROCdf

COLOUR2:PRINTTAB(26,25);"L";:COLOUR3:PRINT"oad";
COLOUR2:PRINTTAB(26,26);"S";:COLOUR3:PRINT"ave";
COLOUR2:PRINTTAB(26,27);"C";:COLOUR3:PRINT"at";

COLOUR2:PRINT TAB(31,25);"E";:COLOUR3:PRINT "rase";
COLOUR2:PRINT TAB(31,26);"B";:COLOUR3:PRINT "oot";

PROCdm

ENDPROC



DEF PROCdf

FORi%=0TO2

IFi%=M%THENCOLOUR129ELSECOLOUR128
COLOUR2:PRINTTAB(5,25+i%);1+i%;SPC(19);TAB(7,25+i%);:COLOUR3:PRINTf$(i%);

NEXT

COLOUR128

ENDPROC



DEFPROCdc

CC%=CC%-1
*FX19

IFCC%>0THENENDPROC

CC%=10
C%=C%EOR1

IFC%=0THENPROCdt(V%+X%,W%+Y%,18):PROCdt(G%+T%,H%,18)ELSEPROCdmt(X%,Y%):PROCdt(G%+T%,H%,T%)

ENDPROC



DEF PROCpk

IFINKEY(-98)THENIFX%>0THENPROCdmt(X%,Y%):X%=X%-1:C%=1:CC%=1
IFINKEY(-67)THENIFX%<20THENPROCdmt(X%,Y%):X%=X%+1:C%=1:CC%=1
IFINKEY(-73)THENIFY%>0THENPROCdmt(X%,Y%):Y%=Y%-1:C%=1:CC%=1
IFINKEY(-105)THENIFY%<20THENPROCdmt(X%,Y%):Y%=Y%+1:C%=1:CC%=1
IFINKEY(-1)THENPROCpmt(X%,Y%,0):PROCdmt(X%,Y%)
IFINKEY(-74)THENPROCpmt(X%,Y%,T%):PROCdmt(X%,Y%)

k$=INKEY$(0)

IFk$=","THENIFT%>0 THEN PROCdt(G%+T%,H%,T%):C%=1:CC%=1:T%=T%-1:ENDPROC
IFk$="."THENIFT%<17 THEN PROCdt(G%+T%,H%,T%):C%=1:CC%=1:T%=T%+1:ENDPROC

IFk$="1"THENIFM%<>0 THEN M%=0:PROCdf:PROCdm:C%=1:CC%=1:ENDPROC
IFk$="2"THENIFM%<>1 THEN M%=1:PROCdf:PROCdm:C%=1:CC%=1:ENDPROC
IFk$="3"THENIFM%<>2 THEN M%=2:PROCdf:PROCdm:C%=1:CC%=1:ENDPROC

IFk$="L"THENPROClm:ENDPROC
IFk$="S"THENPROCsm:ENDPROC
IFk$="C"THENPROCcat:ENDPROC
IFk$="E"THENm$="erase maze":IFFNc THENPROCem:ENDPROC
IFk$="B"THENm$="boot game":IFFNc THENOSCLI("EXEC !Boot"):END

ENDPROC



DEFPROClm

COLOUR(3)
INPUT TAB(0,29);"Load file name ";l$
PROCb

IFl$<>""THENf%=OPENIN(l$):IFf%=0THENPRINTTAB(0,29);l$;" does not exist";:PROCa:ENDPROC
CLOSE#f%

OSCLI("LOAD "+l$+" "+STR$~(&2F19-M%*&E7))
f$(M%)=l$

f%=OPENOUT("$.Mazes")
PRINT#f%,f$(0),f$(1),f$(2)
CLOSE#f%

PROCdf
PROCdm

ENDPROC



DEFPROCsm

COLOUR(3)
INPUTTAB(0,29);"Save file name ";s$
PROCb

IFs$=""THENs$=f$(M%)

f%=OPENIN(s$):IFf%<>0THENCLOSE#f%:m$="OVERWRITE FILE":IFNOTFNc THENENDPROC

OSCLI("SAVE "+s$+" "+STR$~(&2F19-M%*&E7)+" +E7 FFFFFF 0")
f$(M%)=s$

f%=OPENOUT("$.Mazes")
PRINT#f%,f$(0),f$(1),f$(2)
CLOSE#f%

PROCdf

ENDPROC



DEFPROCcat

CLS

COLOUR 2
*CAT

PROCa

PROCds

ENDPROC



DEFPROCa

COLOUR 3
PRINTTAB(0,30);"Press any key";

k$=GET$

PROCb

ENDPROC



DEFFNc

COLOUR(3)
PRINTTAB(0,29);"Confirm ";m$;" ? Y/N";

k$=GET$
PROCb

=k$="Y"



DEFPROCdm

GCOL0,3
MOVEV%*48-24,1020-W%*32+16
DRAW(V%+21)*48+24,1020-W%*32+16
DRAW(V%+21)*48+24,1020-(W%+21)*32-16
DRAWV%*48-24,1020-(W%+21)*32-16
DRAWV%*48-24,1020-W%*32+16

FORy%=0TO20:FORx%=0TO10:PROCdmt(x%,y%):NEXT,

ENDPROC



DEFPROCdmt(x%,y%)

IFx%>10 THEN x%=20-x%

PROCdt(V%+x%,W%+y%,?(y%*11+x%+&2F19-M%*&E7))
PROCdt(V%+20-x%,W%+y%,?(&2B00+?(y%*11+x%+&2F19-M%*&E7)))

ENDPROC



DEFPROCpmt(x%,y%,t%)

IFx%>10THENx%=20-x%:t%=t%?&2B00

IFt%=0THENx%?(y%*11+&2F19-M%*&E7)=t%
IFt%=1THENIF(x% AND 1)=0THENIF(y% AND 1)=0THENx%?(y%*11+&2F19-M%*&E7)=t%
IFt%=2ORt%=3THENIF(x%AND1)=1AND(y%AND1)=1THENx%?(y%*11+&2F19-M%*&E7)=t%
IFt%=4THENIFy%>=2THENIF(x%AND1)<>0AND(y%AND1)=0THENx%?(y%*11+&2F19-M%*&E7)=t%
IFt%=5THENIFy%<=18THENIF(x%AND1)<>0AND(y%AND1)=0THENx%?(y%*11+&2F19-M%*&E7)=t%
IFt%=6THENIFx%>=2THENIF(x%AND1)=0AND(y%AND1)<>0THENx%?(y%*11+&2F19-M%*&E7)=t%
IFt%=7THENIF(x%AND1)=0AND(y%AND1)<>0THENx%?(y%*11+&2F19-M%*&E7)=t%
IFt%>=8THENIF(x%AND1)<>0OR(y%AND1)<>0THENx%?(y%*11+&2F19-M%*&E7)=t%

ENDPROC



DEFPROCdt(x%,y%,t%)

!&70=t%*24+&2B83:!&74=y%*640+x%*24+&3000:CALL&2B12

ENDPROC



DEFPROCem

FORy%=0TO20:FORx%=0TO10
IF(x%AND1)=0AND(y%AND1)=0THEN?(y%*11+x%+&2F19-M%*&E7)=1ELSE?(y%*11+x%+&2F19-M%*&E7)=0
NEXT,

?(&2F19-M%*&E7+&6C)=&0F:?(&2F19-M%*&E7+&77)=&0C:?(&2F19-M%*&E7+&78)=&00:?(&2F19-M%*&E7+&82)=&0A:?(&2F19-M%*&E7+&83)=&0D:?(&2F19-M%*&E7+&BA)=&00:?(&2F19-M%*&E7+&D0)=&00:?(&2F19-M%*&E7+&E6)=&00

PROCdm

ENDPROC



DEFPROCb

PRINT TAB(0,29);SPC(80);

ENDPROC




DEFPROCi

VDU23,1,0;0;0;0;
VDU19,1,4;0;19,2,5;0;19,3,2;0;

*LOAD E.Editor 2B00

*LOAD E.Tiles 2B83

DIM f$(3)

f%=OPENIN("$.Mazes")
INPUT#f%,f$(0),f$(1),f$(2)
CLOSE#f%

FORZ%=0TO2:OSCLI("LOAD "+f$(Z%)+" "+STR$~(&2F19-Z%*&E7)):NEXT

V%=3:W%=1
G%=4:H%=23

T%=0
X%=0:Y%=0

C%=1:CC%=1

M%=0

m$=""

ENDPROC

